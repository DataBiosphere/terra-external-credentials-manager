plugins {
	id 'idea'

	id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
	id 'org.springframework.boot' version '2.6.4' apply false
	id 'com.diffplug.spotless' version '6.3.0' apply false
	// ^^ for some reason, can't use spotless in multiple subprojects without this ^^
	id 'com.google.cloud.tools.jib' version '3.1.4' apply false
	// ^^ see https://github.com/GoogleContainerTools/jib/blob/master/docs/faq.md#i-am-seeing-method-not-found-or-class-not-found-errors-when-building
  	id 'org.sonarqube' version '3.3' apply false
}

repositories { mavenCentral() }

allprojects {
	dependencyLocking {
		lockAllConfigurations()
	}
}

tasks.register('resolveAndLockAll') {
	doFirst {
		assert gradle.startParameter.writeDependencyLocks : 'This task only works with `--write-locks`'
	}
	doLast {
		configurations.findAll {
			// Add any custom filtering on the configurations to be resolved
			it.canBeResolved
		}.each { it.resolve() }
	}
}

subprojects {
	group = 'bio.terra'
	version = '0.73.0-SNAPSHOT'

	apply plugin: 'idea'
	apply plugin: 'java'
	compileJava { options.release = 11 }

	ext {
		artifactGroup = "${group}.${rootProject.name}"
	}

	apply plugin: 'io.spring.dependency-management'

	dependencyManagement {
		imports {
			mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
		}

		dependencies {
			dependency 'io.swagger.core.v3:swagger-annotations:2.1.13'
			dependency 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.33'
		}
	}

}

idea.project.jdkName = '11'
